buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.5.1"
    }
}
apply plugin: 'com.github.node-gradle.node'
apply plugin: 'com.android.application'

// load keyStore properties to sign the apk
def keystorePropertiesFile = rootProject.file("signing.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'io.phonk'
    flavorDimensions "version"
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        applicationId "io.phonk"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        versionCode project.versionCode
        versionName project.versionName
        multiDexEnabled true
        setProperty("archivesBaseName", "phonk-$versionName")

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        aaptOptions {
            noCompress "tflite"
        }
    }

    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] == null ? null : file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
            zipAlignEnabled true
        }

        debug {
            debuggable true
        }
    }

    productFlavors {
        normal {
            applicationId "io.phonk"
            dimension "version"
            versionName project.versionName + "_normal"
        }

        extended {
            applicationId "io.phonk.extended"
            dimension "version"
            versionName project.versionName + "_extended"
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1']
        }
    }

    lint {
        disable 'InvalidPackage'
    }
}

node {
    download = true
    nodeProjectDir = file("${project.webIDEDir}")
    version = "18.10.0"
}

task buildWebIDE(type: NpmTask) {
    dependsOn npmInstall
    args = ['run', 'cleanBuildAndDeploy']
    // script = file('script.js')
}

task deleteExamples(type: Delete) {
    delete "./src/main/assets/examples"
}

task copyExamples(type: Copy, dependsOn: 'deleteExamples') {
    from project.examplesDir
    into "./src/main/assets/examples"
}

repositories {
    maven { url 'https://github.com/kshoji/USB-MIDI-Driver/raw/master/MIDIDriver/snapshots' }

    mavenCentral()
    google()
}

dependencies {
    implementation project(path: ':phonk_apprunner')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference:1.2.0'
    // implementation 'androidx.emoji:emoji-bundled:1.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'org.reactivestreams:reactive-streams:1.0.3'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
}

preBuild.dependsOn buildWebIDE
preBuild.dependsOn copyExamples